apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

androidExtensions {
  // LayoutContainer support and a Parcelable implementation generator.
  // https://kotlinlang.org/docs/tutorials/android-plugin.html
  experimental = true
}

android {
  compileSdkVersion Versions.compileSdkVersion

  defaultConfig {
    minSdkVersion Versions.minSdkVersion
    targetSdkVersion Versions.targetSdkVersion

    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }

  lintOptions {
    textReport true
    abortOnError false
    textOutput 'stdout'
    warning 'UnusedResources'
    warning 'ResourceType'
    warning 'InvalidPackage'
  }

  kotlinOptions {
    suppressWarnings = true
  }

  sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  packagingOptions {
    exclude 'LICENSE.txt'
    exclude 'META-INF/ASL2.0'
    exclude 'META-INF/LICENSE'
    exclude 'META-INF/LICENSE.txt'
    exclude 'META-INF/NOTICE'
    exclude 'META-INF/NOTICE.txt'
    exclude 'META-INF/rxjava.properties'
    exclude 'META-INF/services/javax.annotation.processing.Processor'
  }
}

dependencies {
  implementation Deps.kotlinStdlib

  Deps.testLibs.each { testImplementation it }
  Deps.androidTestLibs.each { androidTestImplementation it }
  Deps.archTesting.each { androidTestImplementation it }
}
